#!/usr/bin/env -S brash run

const PROJECT_NAME = "Brash"
const PROJECT_PATH = "./src/Brash.Cli/Brash.Cli.csproj"
const OUTPUT_DIR = "target/release"
const CONFIGURATION = "Release"

fn is_option(arg: string): bool
    return arg == "--no-trim" || arg == "--no-single-file" || arg == "--no-ready2run" || arg == "-h" || arg == "--help"
end

fn log(text: string): void
    exec("printf", "%s\n", text)
end

fn show_help(): void
    log([[
Usage: build.bsh [OPTIONS] <platform1> [platform2] ...

Options:
  --no-trim          Disable trimming (faster builds, larger binaries)
  --no-single-file   Publish as multiple files (faster builds, easier debugging)
  --no-ready2run     Disable ReadyToRun compilation (faster builds, slower startup)
  -h, --help         Show this help message

Arguments:
  <platforms>  Space-separated list of platforms to build

Available platforms:
  win-x64, win-x86, win-arm64
  linux-x64, linux-arm64, linux-arm
  osx-x64, osx-arm64

Examples:
  build.bsh win-x64
  build.bsh --no-trim win-x64
  build.bsh --no-single-file --no-trim win-x64
  build.bsh win-x64 linux-x64
  build.bsh osx-x64 osx-arm64]]
)
end

fn file_exists(path_value: string): bool
    let result = exec(
        "sh",
        "-c",
        "if [ -e \"$1\" ]; then printf '1'; else printf '0'; fi",
        "_",
        path_value
    )
    return (int)result == 1
end

fn mkdir_p(path_value: string): void
    exec("mkdir", "-p", path_value)
end

fn remove_dir(path_value: string): void
    exec("rm", "-rf", path_value)
end

fn basename(path_value: string): string
    return exec("basename", path_value)
end

fn starts_with(value: string, prefix: string): bool
    let matched = exec(
        "sh",
        "-c",
        "if [ \"${1#\"$2\"}\" != \"$1\" ]; then printf '1'; else printf '0'; fi",
        "_",
        value,
        prefix
    )
    return (int)matched == 1
end

fn trim(value: string): string
    return value.trim()
end

fn get_output_filename(rid: string): string
    if rid == "win-x64"
        return PROJECT_NAME + "-win_x86-64.exe"
    elif rid == "win-x86"
        return PROJECT_NAME + "-win_x86-32.exe"
    elif rid == "win-arm64"
        return PROJECT_NAME + "-win_arm-64.exe"
    elif rid == "linux-x64"
        return PROJECT_NAME + "-linux_x86-64"
    elif rid == "linux-arm64"
        return PROJECT_NAME + "-linux_arm-64"
    elif rid == "linux-arm"
        return PROJECT_NAME + "-linux_arm-32"
    elif rid == "osx-x64"
        return PROJECT_NAME + "-osx_x86-64"
    elif rid == "osx-arm64"
        return PROJECT_NAME + "-osx_arm-64"
    end

    return PROJECT_NAME + "-" + rid
end

fn get_output_dirname(rid: string): string
    if rid == "win-x64"
        return PROJECT_NAME + "-win_x86-64"
    elif rid == "win-x86"
        return PROJECT_NAME + "-win_x86-32"
    elif rid == "win-arm64"
        return PROJECT_NAME + "-win_arm-64"
    elif rid == "linux-x64"
        return PROJECT_NAME + "-linux_x86-64"
    elif rid == "linux-arm64"
        return PROJECT_NAME + "-linux_arm-64"
    elif rid == "linux-arm"
        return PROJECT_NAME + "-linux_arm-32"
    elif rid == "osx-x64"
        return PROJECT_NAME + "-osx_x86-64"
    elif rid == "osx-arm64"
        return PROJECT_NAME + "-osx_arm-64"
    end

    return PROJECT_NAME + "-" + rid
end

fn get_platform_description(rid: string): string
    if rid == "win-x64"
        return "Windows (64-bit)"
    elif rid == "win-x86"
        return "Windows (32-bit)"
    elif rid == "win-arm64"
        return "Windows ARM64"
    elif rid == "linux-x64"
        return "Linux (64-bit)"
    elif rid == "linux-arm64"
        return "Linux ARM64"
    elif rid == "linux-arm"
        return "Linux ARM"
    elif rid == "osx-x64"
        return "macOS Intel"
    elif rid == "osx-arm64"
        return "macOS Apple Silicon"
    end

    return rid
end

fn enabled_label(value: bool): string
    if value
        return "Enabled"
    end

    return "Disabled"
end

fn build_platform(rid: string, description: string, enable_trimming: bool, enable_single_file: bool, enable_ready_to_run: bool): void
    let temp_dir = OUTPUT_DIR + "/temp_" + rid

    log("")
    log("Building for " + description + " (" + rid + ")...")

    let mut publish_cmd = "dotnet publish " + PROJECT_PATH +
        " -c " + CONFIGURATION +
        " -r " + rid +
        " --self-contained" +
        " -p:UseAppHost=true" +
        " -o " + temp_dir

    if enable_single_file
        publish_cmd = publish_cmd + " -p:PublishSingleFile=True"
    end

    if enable_ready_to_run
        publish_cmd = publish_cmd + " -p:PublishReadyToRun=True"
    end

    if enable_trimming
        publish_cmd = publish_cmd +
            " -p:PublishTrimmed=True" +
            " -p:TrimMode=CopyUsed" +
            " -p:EnableTrimAnalyzer=True" +
            " -warnaserror:IL2*"
    end

    bash(publish_cmd)

    if enable_single_file
        let mut src_file = temp_dir + "/Brash.Cli"
        if starts_with(rid, "win-")
            src_file = temp_dir + "/Brash.Cli.exe"
        end
        if src_file == ""
            panic("Build completed but executable was not found")
        end

        if !file_exists(src_file)
            panic("Build completed but executable was not found at: " + src_file)
        end

        let dest_file = OUTPUT_DIR + "/" + get_output_filename(rid)
        exec("mv", src_file, dest_file)
        remove_dir(temp_dir)

        log("Built successfully -> " + basename(dest_file))
        return
    end

    let dest_dir = OUTPUT_DIR + "/" + get_output_dirname(rid)
    if file_exists(dest_dir)
        remove_dir(dest_dir)
    end

    exec("mv", temp_dir, dest_dir)
    log("Built successfully -> " + basename(dest_dir) + "/")
    return
end

fn main(args: string[]): int
    let mut enable_trimming = true
    let mut enable_single_file = true
    let mut enable_ready_to_run = true

    let mut platform_count: int = 0
    let mut platform_list = ""

    for raw_arg in args
        let arg = (string)raw_arg
        if arg == "-h" || arg == "--help"
            show_help()
            return 0
        elif arg == "--no-trim"
            enable_trimming = false
        elif arg == "--no-single-file"
            enable_single_file = false
        elif arg == "--no-ready2run"
            enable_ready_to_run = false
        else
            platform_count = platform_count + 1
            if platform_list == ""
                platform_list = arg
            else
                platform_list = platform_list + " " + arg
            end
        end
    end

    if platform_count == 0
        log("Error: No platforms specified")
        log("")
        show_help()
        return 1
    end

    log("Building " + PROJECT_NAME)
    log("Configuration:")
    log("  Trimming:     " + enabled_label(enable_trimming))
    log("  Single File:  " + enabled_label(enable_single_file))
    log("  ReadyToRun:   " + enabled_label(enable_ready_to_run))

    if file_exists(OUTPUT_DIR)
        log("Cleaning output directory...")
        remove_dir(OUTPUT_DIR)
    end
    mkdir_p(OUTPUT_DIR)

    log("Building platforms: " + platform_list)

    for raw_arg in args
        let arg = (string)raw_arg
        if !is_option(arg)
            let description = get_platform_description(arg)
            build_platform(arg, description, enable_trimming, enable_single_file, enable_ready_to_run)
        end
    end

    log("")
    log("Build completed successfully")
    log("Output directory: " + OUTPUT_DIR)
    return 0
end
